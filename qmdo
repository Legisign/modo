#!/usr/bin/zsh
#
# qmdo -- compile and install `.qm` files
#
# A variant of `modo` for the Qt localization system. Compiles and
# installs `.ts` files.
#
# 2020-01-17    Qt4 to Qt5; stylistic changes.

emulate -L zsh

# `lrelease` required
if [[ -z $(whence lrelease) ]]; then
    print -- ${0:t}: asenna ensin libqt5-linguist 1>&2
    exit 1
fi

# Superuser privileges asked when required
if (( $UID == 0 )); then
    print -- ${0:t}: älä aja tätä ohjelmaa kokonaan pääkäyttäjänä 1>&2
    exit 1
fi

local dest temp qmfile tsfile
local -a instopts tsfiles

# Constants
temp=/tmp/$(whoami)
instopts=(--backup=numbered -m 444)

# Set the target dir (ALWAYS requires `qmdo.def`)
if [[ ! -e qmdo.def ]]; then
    print -- ${0:t}: kansiossa ei ole qmdo.def-tiedostoa 1>&2
    exit 1
else
    dest=$(<qmdo.def)
    print -- ${0:t}: kohdehakemisto "$dest"
    if [[ ! -d $dest ]]; then
        print -- ${0:t}: kohdehakemisto puuttuu\! 1>&2
        exit 1
    fi
fi

# Parse the source files
if (( $# > 0 )); then
    tsfiles=($@)
else
    for tsfile in *.ts(.N); do
        qmfile=$dest/${tsfile:t:r}.mo
        [[ ! -f $qmfile || $tsfile -nt $qmfile ]] && tsfiles=($tsfiles $tsfile)
    done
fi
if (( $#tsfiles == 0  )); then
    print -- ${0:t}: Ei lähdetiedostoja 1>&2
    exit 0
fi

# Set the temp dir
if [[ ! -d $temp ]]; then
    if ! mkdir $temp > /dev/null 2>&1; then
        print -- ${0:t}: Väliaikaiskansion luonti ei onnistu 1>&2
        exit 3
    fi
elif ls -1 $temp | grep -q "\.qm\$"; then
    rm $temp/*.qm
fi

# Compile source files
for tsfile in $tsfiles; do
    qmfile=$temp/${tsfile:t:r}.qm
    print -- ${0:t}: Käännetään ${tsfile:t}...

    # Install target files
    if lrelease $tsfile -qm $qmfile > /dev/null 2>&1; then
        print -- ${0:t}: Kopioidaan ${qmfile:t} -\> $dest...
        sudo install $instopts $qmfile $dest && rm $qmfile
    else
        print -- ${0:t}: Käännösvirhe: "${tsfile:t}"\! 1>&2
    fi
done
