#!/usr/bin/zsh
#
# podo -- decompile Gettext localization files
#
# Usage: podo mofile ... target-dir
#
# 2022-08-24    With only one parameter, use current dir as the destination.

emulate -L zsh

declare dest mofile
declare -a mofiles usage

zparseopts -E -D h=usage -help=usage

if [[ -n $usage ]]; then
    <<EOF
${0:t} -- decompile Gettext localization files

Usage
    ${0:t} [ -h | --help | mofile | mofile... target ]

    "target" is a directory where the .po files are copied.
    With only one parameter, current directory is assumed.

Options
    -h, --help  show the help and quit

EOF
    exit 0
fi

if (( EUID == 0 )); then
    print -- ${0:t}: älä suorita pääkäyttäjäoikeuksin 1>&2
    exit 1
fi

# msgfmt required
if [[ -z $(whence msgunfmt) ]]; then
    print -- ${0:t}: asenna ensin gettext 1>&2
    exit 1
fi

# Set source files and target dir; with one file, default is .
if [[ $# == 1 ]]; then
    mofiles=$*
    dest=.
else
    mofiles=($@[1,-2])
    dest=$@[$#]
    [[ -d $dest ]] || { print -- ${0:t}: $dest ei ole kansio 1>&2; exit 1 }
fi

# Compile files
for mofile in $mofiles; do
    print -- ${0:t}: "$mofile" -\> "$dest"
    if [[ ! -r $mofile || $mofile != *.mo ]]; then
        print -- ${0:t}: "$mofile" ei ole .mo-tiedosto tai ei voi lukea 1>&2
        exit 1
    fi
    msgunfmt -o $dest/${mofile:t:r}.po $mofile
done
