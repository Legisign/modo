#!/usr/bin/zsh
#
# modo -- käännä ja asenna Gettextin käännöstiedostot
#
# 2016-10-03    Edelleen tyylikorjauksia. (TN)

emulate -L zsh

# Vakiot
hash -d modotemp=/tmp/$(whoami)
instopts=(--backup=numbered -o root -g root -m 444)

# Tarvitaan msgfmt
if [[ -z $(whence msgfmt) ]]; then
    print -- ${0:t}: Asenna ensin gettext 1>&2
    exit 1
fi

# Asetetaan kohdekansio
if [[ -r modo.def ]]; then
    dest=$(<modo.def)
elif lsb_release -sd 2> /dev/null | grep -q Ubuntu; then
    dest=/usr/share/locale-langpack/fi/LC_MESSAGES
else
    dest=/usr/share/locale/fi/LC_MESSAGES
fi
print -- ${0:t}: Kohdekansio: "$dest"
if [[ ! -d $dest ]]; then
    print -- ${0:t}: ...kohdekansio puuttuu, yritetään luoda
    if ! sudo mkdir -p $dest; then
        print -- ${0:t}: ...epäonnistui\! 1>&2
        exit 2
    fi
    sudo chmod -R 755 $dest
fi

# Kerätään lähdetiedostot
typeset -a pofiles
if [[ $# -gt 0 ]]; then
    pofiles=($@)
else
    for pofile in *.po(.N); do
        mofile=$dest/${pofile:t:r}.mo
        [[ ! -f $mofile || $pofile -nt $mofile ]] && pofiles=($pofiles $pofile)
    done
fi
if [[ ${#pofiles} -eq 0 ]]; then
    print -- ${0:t}: Ei lähdetiedostoja, lopetetaan 1>&2
    exit 0
fi

# Asetetaan väliaikaiskansio
if [[ ! -d ~modotemp ]]; then
    if ! mkdir ~modotemp > /dev/null 2>&1; then
        print -- ${0:t}: Väliaikaiskansion luonti ei onnistu 1>&2
        exit 3
    fi
# Tyhjennetään kansio .mo-tiedostoista
elif ls -1 ~modotemp | grep -q "\.mo\$"; then
    rm ~modotemp/*.mo
fi

# Käännetään lähdetiedostot
for pofile in $pofiles; do
    poname=${pofile:t}
    mofile=~modotemp/${poname:r}.mo
    print -- ${0:t}: Käännetään $poname...
    msgfmt -o $mofile $pofile
done

# Asennetaan kohdetiedostot
if ls -1 ~modotemp | grep -q "\.mo\$"; then
    for mofile in ~modotemp/*.mo(.); do
        print -- ${0:t}: Kopioidaan $mofile -\> $dest...
        sudo install $instopts $mofile $dest && rm $mofile
    done
else
    print -- ${0:t}: Ei mitään asennettavaa 1>&2
fi
