#!/usr/bin/zsh
#
# modo -- compile and install Gettext localization files
#
# 2023-03-14    Tries to locate the destination directory.

emulate -L zsh

declare d dest force lang modotemp pofile prefix suffix tsfile
declare -a convopts dest1 destfiles instopts l10ndirs pofiles usage verbose
integer destgiven=0

zparseopts -E -D f=force -force=force h=usage -help=usage d:=dest1 -destination:=dest1 v=verbose -verbose=verbose

if [[ -n $usage ]]; then
    <<EOF
${0:t} -- compile and install Gettext localization files

Usage
    ${0:t} [options] pofile...

    Compiles and installs each pofile. The target directory can be set with
    -d, --destination command line option or in a modo.def file in the current
    directory. The contents of the file is just the directory path.

    Unless explicitly given, the script tries to search the target in the
    following directories:
    -   /usr/share/locale/LANGCODE/LC_MESSAGES
    -   /usr/share/locale/kf5/LANGCODE/LC_MESSAGES
    -   /usr/share/locale-langpack/LANGCODE/LC_MESSAGES
    If an older file is not found and the -f, --force flag is not given,
    a warning is reported and nothing is done for the given file.

    LANGCODE is taken from \$LANG by discarding the country code and encoding
    (default now: "${LANG%%_*}").

Options
    -d DIR, --destination=DIR   set target directory
    -f, --force                 install file even if dir not given or found
    -v, --verbose               show the operations
    --help                      show this help and quit

EOF
    exit 0
fi

# Constants
lang=${LANG%%_*}
convopts=(-target-language $lang)
modotemp=/tmp/$(whoami)
instopts=(--backup=numbered -o root -g root -m 444)
prefix=/usr/share/locale
suffix=LC_MESSAGES
l10ndirs=($prefix/$lang/$suffix $prefix/kf5/$lang/$suffix $prefix-langpack/$lang/$suffix)

# msgfmt required
if [[ -z $(whence msgfmt) ]]; then
    print -- ${0:t}: VIRHE: asenna ensin gettext 1>&2
    exit 1
fi

# If target directory is explicitly given on the command line...
if (( $#dest1 > 0 )); then
    destgiven=1
    dest=${dest1[-1]##=}
# Or if there's a readable modo.def...
elif [[ -r modo.def ]]; then
    destgiven=1
    dest=$(<modo.def)
fi
# If the target dir is given, make sure it exists
if (( $destgiven )); then
    [[ -n $verbose ]] && print -- ${0:t}: Kohdekansio: "$dest"
    if [[ ! -d $dest ]]; then
        [[ -n $verbose ]] && print -- ${0:t}: yritetään luoda kohdekansio
        if ! sudo mkdir -p $dest; then
            print -- ${0:t}: kansiota "$dest" ei voitu luoda 1>&2
            exit 2
        fi
        sudo chmod -R 755 $dest
    fi
elif [[ -n $verbose ]]; then
    print -- ${0:t}: Tarkistetaan kohdekansio lennossa
fi

# Collect source files
if (( $# > 0 )); then
    pofiles=($@)
else
    for pofile in *.po(.N); do
        mofile=$dest/${pofile:t:r}.mo
        [[ ! -f $mofile || $pofile -nt $mofile ]] && pofiles+=($pofile)
    done
fi
(( $#pofiles > 0 )) || { print -- ${0:t}: ei lähdetiedostoja 1>&2; exit 0 }
[[ $pofiles == *.po ]] || { print -- ${0:t}: kaikki lähteet eivät ole .po-tiedostoja 1>&2; exit 1 }
[[ -n $verbose ]] && print -- ${0:t}: Lähdetiedostot: "$pofiles"

# Set temp directory
[[ -n $verbose ]] && print -- ${0:t}: Väliaikaiskansio: "$modotemp"
if [[ ! -d $modotemp ]]; then
    [[ -n $verbose ]] && print -- ${0:t}: ...puuttuu, luodaan
    if ! mkdir $modotemp > /dev/null 2>&1; then
        print -- ${0:t}: väliaikaiskansiota "$modotemp" ei voitu luoda 1>&2
        exit 3
    elif [[ -n $verbose ]]; then
        print -- ${0:t}: ...onnistui
    fi
# Remove left-over `.mo` files
elif ls -1 $modotemp | grep -q "\.mo\$"; then
    rm $modotemp/*.mo
fi

# Compile source files
destfiles=()
for pofile in $pofiles; do
    # *_qt.po files need to be compiled to *.qm
    if [[ $pofile == *_qt.po ]]; then
        tsfile=$modotemp/${pofile:t:r}.ts
        mofile=$modotemp/${pofile:t:r}.qm
        [[ -n $verbose ]] && print -- ${0:t}: Tehdään välitiedosto ${tsfile:t}...
        lconvert $convopts -i $pofile -o $tsfile
        if (( $? == 0 )); then
            [[ -n $verbose ]] && print -- ${0:t}: Käännetään ${tsfile:t}...
            lrelease $tsfile -qm $mofile > /dev/null 2>&1 && destfiles+=($mofile)
        else
            print -- ${0:t}: muunnos TS-tiedostoksi epäonnistui 1>&2
            continue
        fi
    # Ordinary *.po files
    elif [[ $pofile == *.po ]]; then
        mofile=$modotemp/${pofile:t:r}.mo
        [[ -n $verbose ]] && print -- ${0:t}: Käännetään ${pofile:t}...
        msgfmt -o $mofile $pofile && destfiles+=($mofile)
    # Not a *.po file
    else
        [[ -n $verbose ]] && print -- ${0:t}: ohitetaan "$pofile"
    fi
done

# Install target files
if (( $#destfiles > 0 )); then
    for mofile in $destfiles; do
        # If destination NOT given with -d, --destination or
        # the modo.def file...
        if (( ! $destgiven )); then
            dest=
            # Find a previous version of the file to be installed
            for d in $l10ndirs; do
                if [[ -e $d/${mofile:t} ]]; then
                    dest=$d
                    break
                fi
            done
            # If destination is still not found, give up,
            # unless -f, --force also given
            if [[ -z $dest ]]; then
                if [[ -z $force ]]; then
                    print -- ${0:t}: kohdekansiota ei löydy, ohitetaan: ${mofile:t}
                    continue
                else
                    dest=$l10ndirs[1]
                    [[ -n $verbose ]] && print -- ${0:t}: kohdekansiota ei löydy, käytetään oletusta: ${mofile:t}
                fi
            fi
        fi
        [[ -n $verbose ]] && print -- ${0:t}: Kopioidaan $mofile -\> $dest...
        sudo install $instopts $mofile $dest && rm $mofile
    done
elif [[ -n $verbose ]]; then
    print -- ${0:t}: ei mitään asennettavaa
fi
