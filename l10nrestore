#!/bin/zsh
#
# l10nrestore -- restore an .mo or a .qm file from a numbered backup
#
# 2016-09-13    Initial Python version. (TN)
# 2020-01-15    Conversion to zsh. (TN)

emulate -L zsh

declare -a backups
local f dryrun prg stepvar verbose
integer point steps

prg=${0:t}

zparseopts -D n=dryrun s:=stepvar v=verbose
(( $#stepvar > 1 )) && steps=${stepvar[2]} || steps=1

if (( $# == 0 )); then
    print -- ${prg}: anna tiedostonimet 1>&2
    return 1
fi

for f in $*; do
    if [[ $f != *.(mo|qm) ]]; then
        print -- ${prg}: "$f": ei .mo- tai .qm-tiedosto, ohitetaan 1>&2
        continue
    fi
    backups=( ${f}.~<->~ )
    if (( $#backups < steps )); then
        print -- ${prg}: "$f": enintään $#backups askelta palautettavissa 1>&2
        continue
    fi

    # 1) rename the latest .mo to put it out of the way
    if [[ -e $f.restore ]]; then
        print -- ${prg}: "$f.restore" on jo olemassa 1>&2
        continue
    fi
    [[ -n $verbose ]] && print -- 1 :: $f → $f.restore
    [[ -n $dryrun ]] || mv $f $f.restore || return 1

    # 2) rename the steps-level backup to .mo
    (( point=$#backups - steps + 1 ))
    [[ -n $verbose ]] && print -- 2 :: ${f}.~${point}~ → $f
    [[ -n $dryrun ]] || mv ${f}.~${point}~ $f || return 1

    # 3) remove newer levels
    if (( point < $#backups )); then
        [[ -n $verbose ]] && print -- 3 :: poistetaan ${f}.~{$(( point + 1 ))..$#backups}~
        [[ -n $dryrun ]] || rm ${f}.~{$(( point + 1 ))..$#backups}~ || return 1
    fi

    # 4) remove the restoration backup
    [[ -n $verbose ]] && print -- 4 :: poistetaan ${f}.restore
    [[ -n $dryrun ]] || rm ${f}.restore
done
